// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Increment val
    Inc16(in=regval,out=incval);
    // Inc or Not
    Mux16(a=regval,b=incval,sel=inc,out=afterinc);
    // Load New or Keep prev value
    Mux16(a=afterinc,b=in,sel=load,out=afterload);
    // Reset or Not
    Mux16(a=afterload,b=false,sel=reset,out=afterreset);
    // Register: final output
    Register(in=afterreset,load=true,out=regval,out=out);
}
